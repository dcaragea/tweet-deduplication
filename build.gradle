plugins {
    id 'net.researchgate.release' version '2.3.3'
}

group 'com.shekhargulati.deduplication.tweet'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "jacoco"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "com.shekhargulati.urlcleaner:urlcleaner:0.3.0"
    compile "com.shekhargulati.reactivex:rx-tweet-stream:0.1.0"

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.slf4j:slf4j-simple:1.7.18'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv('OSS_USER'), password: System.getenv('OSS_PASSWORD'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getenv('OSS_USER'), password: System.getenv('OSS_PASSWORD'))
            }

            pom.project {
                name 'UrlCleaner'
                packaging 'jar'
                artifactId 'urlcleaner'
                description 'A library providing URL normalization and canonicalization capabilities'
                url 'https://github.com/shekhargulati/urlcleaner'

                scm {
                    connection 'https://github.com/shekhargulati/urlcleaner.git'
                    developerConnection 'https://github.com/shekhargulati/urlcleaner.git'
                    url 'https://github.com/shekhargulati/urlcleaner.git'
                }

                licenses {
                    license {
                        name 'MIT'
                    }
                }

                developers {
                    developer {
                        id 'shekhargulati'
                        name 'Shekhar Gulati'
                        email 'shekhargulati84@gmail.com'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport